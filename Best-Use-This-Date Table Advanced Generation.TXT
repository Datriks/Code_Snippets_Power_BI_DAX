This must be created with the help of  Table Tools --New Table ---insert the code and
after that press Mark as Date Table
---------------------------------------------------------------------------------
Dates = 
VAR FirstFiscalMonth = 7  -- First month of fiscal year
VAR FirstDayOfWeek = 0    -- 0 = Sunday, 1 = Monday, ...
VAR FirstYear =           -- Customize first year to use
    YEAR ( MIN ( Sales[Order Date]  ))
RETURN
GENERATE (
    FILTER (
        CALENDARAUTO (),
        YEAR ( [Date] ) >= FirstYear
    ),
    VAR Yr = YEAR ( [Date] )             -- Year Number
    VAR Mn = MONTH ( [Date] )            -- Month Number (1-12)
    VAR Qr = QUARTER ( [Date] )          -- Quarter Number (1-4)
    VAR MnQ = Mn - 3 * (Qr - 1)          -- Month in Quarter (1-3)
    VAR Wd = WEEKDAY ( [Date], 1 ) - 1   -- Week day number (0 = Sunday, 1 = Monday, ...)
    VAR Fyr =                            -- Fiscal Year Number
        Yr + 1 * ( FirstFiscalMonth > 1 && Mn >= FirstFiscalMonth )
    VAR Fqr =                            -- Fiscal Quarter (string)
        FORMAT ( EOMONTH ( [Date], 1 - FirstFiscalMonth ), "\QQ" )
    RETURN ROW (
        "Year", DATE ( Yr, 12, 31 ),
        "Year Quarter", FORMAT ( [Date], "\QQ-YYYY" ),
        "Year Quarter Date", EOMONTH ( [Date], 3 - MnQ ),
        "Quarter", FORMAT ( [Date], "\QQ" ),
        "Year Month", EOMONTH ( [Date], 0 ),
        "Month", DATE ( 1900, MONTH ( [Date] ), 1 ),
        "Day of Week", DATE ( 1900, 1, 7 + Wd + (7 * (Wd < FirstDayOfWeek)) ),
        "Fiscal Year", DATE ( Fyr + (FirstFiscalMonth = 1), FirstFiscalMonth, 1 ) - 1,
        "Fiscal Year Quarter", "F" & Fqr & "-" & Fyr,
        "Fiscal Year Quarter Date", EOMONTH ( [Date], 3 - MnQ ),
        "Fiscal Quarter", "F" & Fqr
    )
)
------------------------------------------------------------------------------------------

Date: m/dd/yyyy (8/14/2007), used as a column to mark as
date table
Year: yyyy (2007)
Year Quarter: Text (Q3-2008)
Year Quarter Date: Hidden (9/30/2008)
Quarter: Text (Q1)
Year Month: mmm yyyy (Aug 2007)
Month: mmm (Aug)
Day of Week: ddd (Tue)
Fiscal Year: \F\Y yyyy (FY 2008)
Fiscal Year Quarter: Text (FQ1-2008)
Fiscal Year Quarter Date: Hidden (9/30/2008)
Fiscal Quarter: Text (FQ1)
----------------------------------------------------------------------------------------
The Date table in this pattern has two hierarchies:
Calendar: Year (Year), Quarter (Year Quarter), Month (Year
Month)
Fiscal: Year (Fiscal Year), Quarter (Fiscal Year Quarter),
Month (Year Month)

------------------------------------------------------------------------------------
Create extra Column with this formula
DateWithSales = 'Dates'[Date]<=MAX(Sales[Order Date])

-----------------------------------------------------------------------------------
Create a Measure in Dates Table as follows:
ShowValueforDates =
VAR LastDateWithData =
    CALCULATE (
        MAX ( 'Sales'[Order Date] ),
        REMOVEFILTERS ()
    )
VAR FirstDateVisible =
    MIN ( Dates[Date] )
VAR Result = FirstDateVisible <= LastDateWithData
RETURN
    Result
--------------------------------------------------------------------------------------


















